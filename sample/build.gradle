apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId "net.mediavrog.samples.irr"
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 2016081101
        versionName "1.1.2"
    }

    String securePropertiesPath = project.projectDir.path + "/secure.properties"
    if (new File(securePropertiesPath).exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file(securePropertiesPath)))

        signingConfigs {
            release {
                storeFile file(props['key.store'])
                storePassword props['key.store.password']
                keyAlias props['key.alias']
                keyPassword props['key.alias.password']
            }
        }
    } else {
        System.out.println("WARNING: Could not find secure.properties")
        // Building from IDE's "Run" button
        signingConfigs {
            release {}
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "|D"
            applicationIdSuffix ".debug"
            buildConfigField "boolean", "DEBUG_FLAG", "true"
            buildConfigField "boolean", "LOG", "true"
        }
        staging {
            debuggable true
            versionNameSuffix "|S"
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "DEBUG_FLAG", "false"
            buildConfigField "boolean", "LOG", "true"
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "DEBUG_FLAG", "false"
            buildConfigField "boolean", "LOG", "false"
        }
    }
}


dependencies {
    implementation project (':library')
    implementation "androidx.appcompat:appcompat:1.2.0"
}
